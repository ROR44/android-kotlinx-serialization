buildscript {
    ext.snapshot_version = '1.1-SNAPSHOT'
    ext.kotlinVersion = ext.snapshot_version

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$snapshot_version"
        classpath "org.jetbrains.kotlinx:kotlinx-serialization:$snapshot_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.2'
    }
}

ext {
    libraries = [
            kotlin_stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
            kotlin_stdlib_common: "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion",
            kotlin_stdlib_js: "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion",
            kotlin_test: "org.jetbrains.kotlin:kotlin-test:$kotlinVersion",
            kotlin_test_common: "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion",
            kotlin_test_js: "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion",
            kotlin_reflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
    ]
}

allprojects {
    group 'org.jetbrains.kotlinx'
    version "$snapshot_version"
}

apply plugin: 'kotlin'

configure([project(':jvm'), project(':js')]) {
    apply plugin: 'kotlin-serialization'

    apply plugin: 'maven-publish'

    def varintName = "${rootProject.name}-${project.name}"

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    tasks.withType(Jar) {
        archivesBaseName = varintName
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId project.group
                artifactId varintName
                version project.version

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}